function (add_security_framework TARGET)
  if (APPLE)
    set_target_properties (${TARGET}
                           PROPERTIES XCODE_ATTRIBUTE_CLANG_ENABLE_OBJC_ARC NO)
    target_link_libraries (${TARGET} PUBLIC "-framework Security")
  endif ()
endfunction ()

function (remove_unknown_pragma_warning TARGET)
  if (MSVC)
    # Disable warning C4068: unknown pragma
    target_compile_options (${TARGET} PRIVATE /wd4068)
  endif ()
endfunction ()

function (exclude_objc_from_unity TARGET)
  get_target_property (targetSources ${TARGET} SOURCES)
  foreach (filename ${targetSources})
    get_filename_component (fileExt ${filename} EXT)
    if ("${fileExt}" STREQUAL ".mm")
      set_property (SOURCE ${filename} PROPERTY SKIP_UNITY_BUILD_INCLUSION ON)
    endif ()
  endforeach ()
endfunction ()

function (add_flag TARGET FLAG)
  if (APPLE)
    check_c_compiler_flag (${FLAG} TEST_${FLAG})
    if (TEST_${FLAG})
      message (VERBOSE "Has ${FLAG} available")
      target_compile_options (${TARGET} PRIVATE ${FLAG})
    endif ()
  endif ()
endfunction ()
